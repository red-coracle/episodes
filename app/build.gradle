apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 14
        versionName "0.13.1"
        buildConfigField "String", "TVDB_KEY", "\"E457AE7B767BC9F4\""
    }
    flavorDimensions "flavor"
    productFlavors {
        fdroid {
            dimension "flavor"
            applicationId = "org.jamienicol.episodes"
        }
        standard {
            dimension "flavor"
            applicationId = "com.redcoracle.episodes"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.uwetrottmann.thetvdb-java:thetvdb-java:2.1.0'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
}
